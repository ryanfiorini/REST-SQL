CREATE PROCEDURE [open].[POST_PERSON]
(
	/*Returns Json*/
	/*Single Result*/
	@FIRST_NAME nvarchar(32),
    @LAST_NAME nvarchar(32),
    @EMAIL nvarchar(256),

	@SQL_ERROR_ID BIGINT OUTPUT,
	@MESSAGE_RESULT VARCHAR(256) OUTPUT
)
AS
BEGIN

	SET NOCOUNT ON;

	IF @FIRST_NAME IS NULL THROW 50001, '@FIRST_NAME', 1;
	IF @LAST_NAME IS NULL THROW 50001, '@LAST_NAME', 1;
	IF @EMAIL IS NULL THROW 50001, '@EMAIL', 1;

	DECLARE @RESULT_TABLE TABLE
	(
		PERSON_ID INT
	)

	BEGIN TRY

		IF EXISTS(SELECT 1 FROM dbo.PERSON WHERE EMAIL = @EMAIL)
		BEGIN
			SET @MESSAGE_RESULT = (SELECT 999 AS messageId, 'SOME GENERIC MESSAGE' AS messageText FOR JSON PATH, WITHOUT_ARRAY_WRAPPER);
			RETURN 409
		END

		INSERT INTO [dbo].[PERSON]
        (
		   [FIRST_NAME],
           [LAST_NAME],
           [EMAIL]
		)
		OUTPUT inserted.PERSON_ID INTO @RESULT_TABLE(PERSON_ID)
		VALUES
        (
			@FIRST_NAME,
			@LAST_NAME,
			@EMAIL
		)

		SELECT TOP 1 
			PERSON_ID personId,
			@FIRST_NAME firstName,
			@LAST_NAME lastName,
			@EMAIL email
		FROM @RESULT_TABLE
		FOR JSON PATH, WITHOUT_ARRAY_WRAPPER

		RETURN (200);

	END TRY
	BEGIN CATCH

		SET @SQL_ERROR_ID = 21;
		SET @MESSAGE_RESULT = (SELECT 500 AS messageId FOR JSON PATH, WITHOUT_ARRAY_WRAPPER);
		RETURN (500);

	END CATCH

END;